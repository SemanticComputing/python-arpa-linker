PREFIX text: <http://jena.apache.org/text#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX hipla: <http://ldf.fi/schema/hipla/>
PREFIX places: <http://ldf.fi/warsa/places>
PREFIX ptype: <http://ldf.fi/warsa/places/place_types/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?id ?label ?ngram ?type
{
  {
    GRAPH <http://ldf.fi/warsa/places/municipalities> {
      VALUES ?ngram {
        <VALUES>
      }
      BIND(CONCAT('"',?ngram,'"') AS ?qstring)
      ?id text:query ?qstring ;
          a ?type ;
          rdfs:label|skos:prefLabel ?label .
      FILTER(str(?label) = ?ngram)
    }
  } UNION {
    GRAPH <http://ldf.fi/warsa/places/karelian_places> {
      VALUES ?ngram {
        <VALUES>
      }
      BIND(CONCAT('"',?ngram,'"') AS ?qstring)
      ?id text:query ?qstring ;
          a ?type ;
          rdfs:label|skos:prefLabel ?label .
      FILTER(str(?label) = ?ngram)
      MINUS {
        ?id a ptype:Rakennettu_kohde .
      }
      MINUS {
        ?id a ptype:Symboli .
      }
    }
  } UNION {
    SERVICE <http://ldf.fi/pnr/sparql> {
      VALUES ?ngram {
        <VALUES>
      }
      BIND(CONCAT('"',?ngram,'"') AS ?qstring)
      ?id text:query ?qstring .
      ?id a ?type .
      FILTER(?type = <http://ldf.fi/pnr-schema#place_type_560> ||
        ?type = <http://ldf.fi/pnr-schema#place_type_550> ||
        ?type = <http://ldf.fi/pnr-schema#place_type_540>)
      ?id rdfs:label|skos:prefLabel ?label .
      FILTER(STR(?label)=STR(?ngram))
    }
  }
}
ORDER BY ?id
